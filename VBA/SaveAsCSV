Sub SaveAsCsv()
    Dim wb As Workbook
    Dim sh As Worksheet
    Dim myPath As String
    Dim myFile As String
    Dim myExtension As String
    Dim FldrPicker As FileDialog

    ' Оптимизация скорости выполнения макроса
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.Calculation = xlCalculationManual

    ' Получение пути к папке от пользователя
    Set FldrPicker = Application.FileDialog(msoFileDialogFolderPicker)
    
    Application.StatusBar = "Выбор папки..."

    With FldrPicker
        .Title = "Выберите папку с файлами для конвертации"
        .AllowMultiSelect = False
        If .Show <> -1 Then GoTo NextCode
        myPath = .SelectedItems(1) & "\"
    End With

    ' В случае отмены
NextCode:
    Application.StatusBar = False
    myPath = myPath
    If myPath = "" Then Exit Sub

    ' Целевое расширение файлов (должно включать шаблон "*")
    myExtension = "*.xls*"

    ' Путь к целевым файлам с указанным расширением
    myFile = Dir(myPath & myExtension)

    ' Цикл по каждому Excel-файлу в папке
    Do While myFile <> ""
        Application.StatusBar = "Конвертирую " & myFile & "..."
        
        ' Установка переменной для открытого файла
        Set wb = Workbooks.Open(Filename:=myPath & myFile)
        
        ' Формирование имени выходного файла
        nameWb = myPath & Left(myFile, InStr(1, myFile, ".") - 1) & ".csv"
        
        ' Сохранение файла в формате CSV
        ActiveWorkbook.SaveAs Filename:=nameWb, FileFormat:=xlCSV, Local:=True
        
        ' Закрытие файла без сохранения изменений
        ActiveWorkbook.Close SaveChanges:=False
        Application.StatusBar = False
        
        ' Получение имени следующего файла
        myFile = Dir
    Loop

    ' Сброс настроек оптимизации макроса
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    Application.Calculation = xlCalculationAutomatic
End Sub
